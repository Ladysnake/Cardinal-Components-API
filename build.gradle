import java.nio.charset.StandardCharsets
import java.time.Year

plugins {
    id "moe.nikky.fabric-loom" version "0.2.6-SNAPSHOT"
    id "net.minecrell.licenser" version "0.2.1"
    id "com.matthewprenger.cursegradle" version "1.2.0"
    id "maven-publish"
    id "java-library"
}

group = "com.github.NerdHubMC"
archivesBaseName = "Cardinal-Components-API"

subprojects {
    apply plugin: 'moe.nikky.fabric-loom'
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'com.matthewprenger.cursegradle'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    archivesBaseName = project.name
    group = "${rootProject.group}.${rootProject.archivesBaseName}"

    apply from: "https://raw.githubusercontent.com/NerdHubMC/Gradle-Scripts/master/scripts/fabric/basic_project.gradle"
}

allprojects {

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    version = System.getenv("TRAVIS_TAG") ?: rootProject.mod_version

    repositories {
        maven {
            name = "FabricMC"
            url = "https://maven.fabricmc.net"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.minecraft_version}+build.${rootProject.yarn_mappings}"
        fabricInstall "net.fabricmc:fabric-loader:${rootProject.loader_version}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.0-M1"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.5.0-M1"
        compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    }

    processResources {
        // this will ensure that this task is redone when there"s a change
        inputs.property "version", project.version

        // replace stuff in fabric.mod.json, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include "fabric.mod.json"

            // add mod metadata
            expand "version": project.version
        }

        // copy everything else, that"s not the fabric.mod.json
        from(sourceSets.main.resources.srcDirs) {
            exclude "fabric.mod.json"
        }
    }

    license {
        header = rootProject.file("code_quality/${project.license_header}_HEADER.txt")
        include "**/*.java"
        charset = StandardCharsets.UTF_8.name()

        style {
            java = "BLOCK_COMMENT"
        }
        newLine = false // Disables the empty line between the header and package name
        //ignoreFailures = true //Ignore failures and only print a warning on license violations

        //export variables
        ext {
            year = Year.now()
            projectDisplayName = rootProject.archivesBaseName
        }
    }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

task licenseFormatAll
subprojects { p -> licenseFormatAll.dependsOn("${p.path}:licenseFormat") }
subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

dependencies {
    // used by the test mod
    loom.deobf implementation("net.fabricmc.fabric-api:fabric-api-base:${rootProject.fabric_base_version}")
    loom.deobf implementation("net.fabricmc.fabric-api:fabric-object-builders-v0:${fabric_object_builders_version}")
    loom.deobf runtime("net.fabricmc.fabric-api:fabric-networking-v0:${rootProject.fabric_networking_version}")
    loom.deobf runtime("net.fabricmc.fabric-api:fabric-resource-loader-v0:${fabric_resource_loader_version}")
    loom.deobf runtime("net.fabricmc.fabric-api:fabric-mining-levels-v0:${fabric_mining_levels_version}")
    loom.deobf runtime("net.fabricmc.fabric-api:fabric-tag-extensions-v0:${fabric_tag_extensions_version}")

    include "net.fabricmc.fabric-api:fabric-api-base:${fabric_base_version}"
    afterEvaluate {
        subprojects.each {
            api(project(":${it.name}"))
            include project(":${it.name}")
        }
    }
    //loom.deobf testImplementation(sourceSets.main.output) //FIXME deobf + test doesn't work?
}